PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cedr: <http://cedropendata.mfcr.cz/c3lod/cedr/vocabCEDR#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX time: <http://www.w3.org/2006/time#>

DELETE {
  ?s cedr:smlouvaPodpisDatum ?dateTime .
} INSERT {
  ?s dct:temporal ?abstraction .
  
  ?abstraction a time:Instant ;
    time:inXSDDateTime ?dateTime ;
	rdfs:label ?abstractionLabel .
} WHERE {
  ?s cedr:smlouvaPodpisDatum ?dateTime .
  
  OPTIONAL {
	?s dct:title ?title .
	BIND(CONCAT("Signature of agreement number ", STR(?title)) AS ?abstractionLabel)
  }
  
  BIND(IRI(CONCAT(STR(?s), "/abstraction/cedr-smlouvaPodpisDatum2time-Instant")) AS ?abstraction)
}
